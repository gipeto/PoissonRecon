cmake_minimum_required(VERSION 3.16.4)

# Use ccache if available
find_program(CCACHE_PROGRAM ccache)
if(CCACHE_PROGRAM)
    set_property(GLOBAL PROPERTY RULE_LAUNCH_COMPILE "${CCACHE_PROGRAM}")
endif()

project(AdaptiveMultiGridSolver VERSION 13.72.0 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

# Set project options
include(cmake/options.cmake REQUIRED)

# Let the module path point to the location where the finders will be generated by
# the conan cmake_find_package generator
set(CMAKE_MODULE_PATH ${CMAKE_BINARY_DIR})

# Configure project install folder structure
# and setup RPATHS for UNIX systems
include(GNUInstallDirs)

if(APPLE)
  set(basePoint @loader_path)
else()
  set(basePoint $ORIGIN)
endif()

file(RELATIVE_PATH relDir
  ${CMAKE_CURRENT_BINARY_DIR}/${CMAKE_INSTALL_BINDIR}
  ${CMAKE_CURRENT_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})

set(CMAKE_INSTALL_RPATH ${basePoint} ${basePoint}/${relDir})

# Find required packages
include(cmake/find_dependencies.cmake REQUIRED)
# Setup compiler
include(cmake/compiler_options.cmake REQUIRED)

add_subdirectory(src)
